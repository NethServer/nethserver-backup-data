#!/usr/bin/perl -w

#
# Copyright (C) 2013 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use esmith::ConfigDB;
use File::Temp;
use Time::localtime;
use NethServer::BackupData;

use constant CMD => '/usr/bin/duplicity';

my $confdb = esmith::ConfigDB->open;
my $systemname = $confdb->get('SystemName') or die "No SystemName db entry found\n";
$systemname = $systemname->prop('type');
my $backupwk = $confdb->get('backup-data') or die "No backup-data db entry found\n";

my $tm = localtime(time);
my $dow = $tm->wday;

my $type = $backupwk->prop('Type') || 'full';
my $VFSType = $backupwk->prop('VFSType') || 'UNKNOWN';
my $fullday = $backupwk->prop('FullDay') || 0;
my $mntdir = $backupwk->prop('Mount') || '/mnt/backup';
my $logFile = $backupwk->prop('LogFile') || '/var/log/last-backup.log';
my $volSize = $backupwk->prop('VolSize') || '250';
my $opts = '--allow-source-mismatch';
my $dst = "";
my $cacheDir = "/var/lib/nethserver/backup/duplicity/";

our $backup = new NethServer::BackupData();

if ($type eq 'full') {
     # do full
     $opts .= " full ";
} elsif ($type eq 'incremental') {
    if ($dow == $fullday) { #force full on selected week day
        # do full
        $opts .= " full ";
    } else {
        # do incremental
        $opts .= " incr ";
    }
} else { # nothing to do
    $backup->logger('ERROR',"Backup type unknown - 1");
    exit 1;
}


# Read include and exclude from config directory
my @include_data_files = $backup->includes(NethServer::BackupData::CONF_DIR);
my @exclude_data_files = $backup->excludes(NethServer::BackupData::CONF_DIR);

if (!@include_data_files) {
    $backup->logger('ERROR',"Nothing to backup - 1");
    exit 1;
}

# Create globbing file list
my $tmp = File::Temp->new(UNLINK => 1);
chmod(0600,$tmp->filename);

$backup->uniq(\@include_data_files);
$backup->uniq(\@exclude_data_files);

foreach  (@exclude_data_files) {
   print $tmp "- $_\n";
}
foreach  (@include_data_files) {
   print $tmp "+ $_\n";
}
print $tmp "- **\n";

$opts .= " --exclude=$cacheDir --include-filelist $tmp";
$opts .= " --volsize $volSize ";

if ( ($VFSType eq 'usb') || ($VFSType eq 'nfs') || ($VFSType eq 'cifs') || ($VFSType eq 'webdav') ) {
   mkdir "$mntdir/$systemname";
   $dst = " file://$mntdir/$systemname ";
} else {
   $backup->logger("ERROR","No valid VFSType ($VFSType) - 1");
   exit 1;
}

# force full backup if not exists > 8 days
$opts .= " --full-if-older-than 8D ";
# disable encryption
$opts .= " --no-encryption ";
# set duplicity cache dir
$opts .= " --archive-dir $cacheDir ";

# remove stale duplicyty lockfile
unlink glob "$cacheDir/*/lockfile.lock";

# Debug 
# $opts .= " --dry-run -v8 ";
# print $tmp->filename."\n";
# print CMD." $opts / $dst &>$logFile\n";

my $ret = system(CMD." $opts / $dst &>$logFile");

if ($ret != 0) {
   $backup->logger("ERROR","Backup failed, see $logFile for details - $ret");
   exit 1; 
}

exit (0);
